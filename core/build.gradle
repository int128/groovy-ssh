plugins {
    id 'java-library'
    id 'groovy'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

dependencies {
    api localGroovy()
    api 'com.github.mwiede:jsch:0.2.5'
    api 'org.slf4j:slf4j-api:2.0.6'

    testImplementation platform("org.spockframework:spock-bom:2.3-groovy-3.0")
    testImplementation "org.spockframework:spock-core"
    testImplementation "org.spockframework:spock-junit4"
    testRuntimeOnly 'cglib:cglib-nodep:3.2.10'
    testRuntimeOnly 'org.objenesis:objenesis:3.0.1'
    testRuntimeOnly 'ch.qos.logback:logback-classic:1.4.5'
}

test {
    useJUnitPlatform()
}

processResources {
    filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: ['version': project.version])
}

java {
    withJavadocJar()
    withSourcesJar()
}

description = 'Groovy SSH library'

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/int128/groovy-ssh"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }

    publications {
        maven(MavenPublication) {
            artifactId parent.name
            from components.java
            pom {
                name = parent.name
                description = project.description
                url = 'https://github.com/int128/groovy-ssh'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }
}